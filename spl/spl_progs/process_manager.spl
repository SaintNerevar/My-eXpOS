alias functionNum R1;

if (functionNum == EXIT_PROCESS) then
    alias currentPID R2;

    multipush(R1, R2);
    R1 = FREE_PAGE_TABLE;
    R2 = currentPID;
    call PROCESS_MANAGER;
    multipop(R1, R2);

    multipush(R1, R2);
    R1 = FREE_USER_AREA_PAGE;
    R2 = currentPID;
    call PROCESS_MANAGER;
    multipop(R1, R2);

    if ([PROCESS_TABLE + currentPID*16 + 9] != INT_EXEC) then
        alias PID R3;
        PID = 1;
        while (PID < MAX_PROC_NUM) do
            if ([PROCESS_TABLE + PID*16 + 4] == WAIT_PROCESS
                && [PROCESS_TABLE + PID*16 + 5] == currentPID
            ) then
                [PROCESS_TABLE + PID*16 + 4] = READY;
            endif;

            if ([PROCESS_TABLE + PID*16 + 2] == currentPID) then
                [PROCESS_TABLE + PID*16 + 2] = -1;
            endif;
            PID = PID + 1;
        endwhile;
    endif;

    [PROCESS_TABLE + currentPID*16 + 4] = TERMINATED;
    return;
endif;

if (functionNum == FREE_PAGE_TABLE) then
    alias currentPID R2;
    alias page_table_entry R3;

    page_table_entry = PAGE_TABLE_BASE + currentPID*20;
    [page_table_entry] = -1;
    [page_table_entry + 1] = "0000";
    [page_table_entry + 2] = -1;
    [page_table_entry + 3] = "0000";

    alias pageNum R4;
    pageNum = 2;
    while (pageNum < 10) do
        //Free pages only if they are valid
        if ([page_table_entry + 2*pageNum + 1] != "0000") then
            multipush(R1, R2, R3, R4);
            R1 = RELEASE_PAGE;
            R2 = [page_table_entry + 2*pageNum];
            call MEMORY_MANAGER;
            multipop(R1, R2, R3, R4);

            [page_table_entry + 2*pageNum] = -1;
            [page_table_entry + 2*pageNum + 1] = "0000";
        endif;

        pageNum = pageNum + 1;
    endwhile;

    alias counter R5;
    alias diskMapTable R6;
    diskMapTable = DISK_MAP_TABLE + currentPID*10;
    counter = 0;
    while (counter < 10) do
        if (counter >= 4 && counter <=7) then
            [diskMapTable + counter] = -1;
        endif;

        if ([diskMapTable + counter] != -1) then
            multipush(R1, R2, R3, R4, R5, R6);
            R1 = RELEASE_BLOCK;
            R2 = [diskMapTable + counter];
            call MEMORY_MANAGER;
            multipop(R1, R2, R3, R4, R5, R6);
            [diskMapTable + counter] = -1;
        endif;
        counter = counter + 1;
    endwhile;
    return;
endif;

if (functionNum == FREE_USER_AREA_PAGE) then
    alias currentPID R2;
    alias process_table_entry R3;

    process_table_entry = PROCESS_TABLE + currentPID*16;
    
    alias pprt R4;
    alias resourceID R5;
    pprt = [process_table_entry + 11]*512 + 496;
    resourceID = 0;

    while (resourceID < 8) do
        if ([pprt + 2*resourceID] == SEMAPHORE) then
            multipush(R1, R2, R3, R4, R5);
            R1 = RELEASE_SEMAPHORE;
            R3 = currentPID;
            R2 = [pprt + 2*resourceID + 1];
            call RESOURCE_MANAGER;
            multipop(R1, R2, R3, R4, R5);
        endif;

        if ([pprt + 2*resourceID] == FILE) then
            multipush(R1, R2, R3, R4, R5);
            R1 = CLOSE;
            R2 = [pprt + 2*resourceID + 1];
            call FILE_MANAGER;
            multipop(R1, R2, R3, R4, R5);
        endif;
        resourceID = resourceID + 1;
    endwhile;

    multipush(R1, R2, R3);
    R1 = RELEASE_PAGE;
    R2 = [process_table_entry + 11];
    call MEMORY_MANAGER;
    multipop(R1, R2, R3);

    return;
endif;

if (functionNum == GET_PCB_ENTRY) then
    alias PID R2;
    alias process_table_entry R3;
    PID = 2;

    while (PID < MAX_PROC_NUM) do
        process_table_entry = PROCESS_TABLE + PID*16;
        if ([process_table_entry + 4] == TERMINATED) then
            break;
        endif;
        PID = PID + 1;
    endwhile;

    if (PID != MAX_PROC_NUM) then
        [process_table_entry + 1] = PID;
        [process_table_entry + 4] = ALLOCATED;
        [process_table_entry + 14] = PAGE_TABLE_BASE + PID*20;
        [process_table_entry + 15] = 10;
        R0 = PID;
    else
        R0 = -1;
    endif;
    return;
endif;


if (functionNum == KILL_ALL) then
    alias currentPID R2;

    alias inode_index R3;
    inode_index = 0;

    while (inode_index < MAX_FILE_NUM) do
        if ([INODE_TABLE + 16*inode_index + 1] != -1) then
            multipush(R1, R2, R3);
            R1 = ACQUIRE_INODE;
            R2 = inode_index;
            R3 = [SYSTEM_STATUS_TABLE + 1];
            call RESOURCE_MANAGER;
            multipop(R1, R2, R3);
        endif;
        inode_index = inode_index + 1;
    endwhile;

    alias pid R4;
    pid = 2;

    while (pid < MAX_PROC_NUM-1) do
        if (pid != currentPID 
            && [PROCESS_TABLE + 16*pid + 4] != TERMINATED
        )then
            multipush(R1, R2, R3, R4);
            R1 = EXIT_PROCESS;
            R2 = pid;
            call PROCESS_MANAGER;
            multipop(R1, R2, R3, R4);
        endif;
        pid = pid + 1;
    endwhile;

    inode_index = 0;
    while (inode_index < MAX_FILE_NUM) do
        if ([INODE_TABLE + 16*inode_index + 1] != -1) then
            multipush(R0, R1, R2, R3, R4);
            R1 = RELEASE_INODE;
            R2 = inode_index;
            R3 = [SYSTEM_STATUS_TABLE + 1];
            call RESOURCE_MANAGER;
            multipop(R0, R1, R2, R3, R4);
        endif;
        inode_index = inode_index + 1;
    endwhile;

    return;
endif;